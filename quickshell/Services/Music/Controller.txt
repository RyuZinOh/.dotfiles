import QtQuick
import QtQuick.Layouts
import Qt5Compat.GraphicalEffects
import Quickshell.Services.Mpris
import qs.Services.Shapes

Item {
    id: root
    width: content.width
    height: 200

    property bool isHovered: false
    property var player: null
    property bool isPlaying: player ? player.playbackState === MprisPlaybackState.Playing : false
    property bool hasPlayer: player !== null

    //detect everything out there available
    Repeater {
        model: Mpris.players
        Item {
            Component.onCompleted: {
                if (!root.player) {
                    root.player = modelData; //first one
                }
            }
        }
    }

    Timer {
        running: root.isPlaying
        interval: 500 // update 0.5sec [progress bar]
        repeat: true
        onTriggered: {
            if (root.player) {
                progressBar.currentPosition = root.player.position || 0;
            }
        }
    }

    PopoutShape {
        id: content
        anchors.right: parent.right
        anchors.verticalCenter: parent.verticalCenter
        width: root.isHovered ? 380 : 0.1
        height: 200
        style: 1
        alignment: 2 //right Shape
        radius: 20
        color: "black"

        Behavior on width {
            NumberAnimation {
                duration: 450
                easing.type: Easing.OutCubic
            }
        }

        Text {
            anchors.centerIn: parent
            text: "Nothing playing..."
            font.family: "CaskaydiaCove NF"
            font.pixelSize: 14
            color: "gray"
            visible: !root.hasPlayer
            opacity: root.isHovered ? 1 : 0
            Behavior on opacity {
                NumberAnimation {
                    duration: 300
                }
            }
        }

        RowLayout {
            anchors.fill: parent
            anchors.margins: 16
            spacing: 16
            visible: root.hasPlayer
            opacity: root.isHovered ? 1 : 0

            Behavior on opacity {
                NumberAnimation {
                    duration: 300
                }
            }

            Item {
                Layout.preferredWidth: 80
                Layout.preferredHeight: 80
                Layout.alignment: Qt.AlignVCenter

                Rectangle {
                    anchors.centerIn: parent
                    width: 80
                    height: 80
                    radius: 56
                    border.color: "white"
                    border.width: 2

                    Item {
                        anchors.fill: parent
                        anchors.margins: 6

                        Image {
                            id: albumArt
                            anchors.fill: parent
                            source: root.player ? (root.player.trackArtUrl || "") : ""
                            fillMode: Image.PreserveAspectCrop
                            smooth: true
                            asynchronous: true
                            cache: true
                            visible: false
                        }

                        Rectangle {
                            id: mask
                            anchors.fill: parent
                            radius: width / 2
                            visible: false
                        }

                        OpacityMask {
                            anchors.fill: albumArt
                            source: albumArt
                            maskSource: mask
                        }

                        Text {
                            anchors.centerIn: parent
                            text: "󰝚" // ts appears when the thumb are not there
                            font.family: "CaskaydiaCove NF"
                            font.pixelSize: 42
                            color: "white"
                            visible: !root.player || !root.player.trackArtUrl
                        }
                    }
                }
            }

            ColumnLayout {
                Layout.fillWidth: true
                Layout.fillHeight: true
                spacing: 8

                Text {
                    Layout.fillWidth: true
                    text: root.player ? (root.player.trackTitle || "Unknown Title") : ""
                    font.family: "CaskaydiaCove NF"
                    font.pixelSize: 12
                    font.weight: Font.Bold
                    color: "white"
                    elide: Text.ElideRight
                    maximumLineCount: 1
                }

                Text {
                    Layout.fillWidth: true
                    text: root.player ? (root.player.trackArtist || "Unknown Artist") : ""
                    font.family: "CaskaydiaCove NF"
                    font.pixelSize: 10
                    color: "silver"
                    elide: Text.ElideRight
                    maximumLineCount: 1
                }

                Item {
                    Layout.fillHeight: true
                }

                Rectangle {
                    Layout.fillWidth: true
                    Layout.preferredHeight: 4
                    radius: 2
                    color: "black"

                    //progress bar
                    Rectangle {
                        id: progressBar
                        height: parent.height
                        radius: 2
                        color: "white"

                        property real currentPosition: root.player ? (root.player.position || 0) : 0
                        property real trackLength: root.player ? (root.player.length || 1) : 1

                        width: trackLength > 0 ? Math.min((currentPosition / trackLength) * parent.width, parent.width) : 0

                        Behavior on width {
                            SmoothedAnimation {
                                duration: 500
                                velocity: -1
                            }
                        }
                    }
                }

                /*
                 - current
                 - elapsed
                 - total
                 */
                RowLayout {
                    Layout.fillWidth: true

                    Text {
                        text: formatTime(progressBar.currentPosition)
                        font.family: "CaskaydiaCove NF"
                        font.pixelSize: 10
                        color: "whitesmoke"
                    }

                    Item {
                        Layout.fillWidth: true
                    }

                    Text {
                        text: formatTime(progressBar.trackLength - progressBar.currentPosition)
                        font.family: "CaskaydiaCove NF"
                        font.pixelSize: 10
                        color: "whitesmoke"
                    }

                    Item {
                        Layout.fillWidth: true
                    }

                    Text {
                        text: formatTime(progressBar.trackLength)
                        font.family: "CaskaydiaCove NF"
                        font.pixelSize: 10
                        color: "whitesmoke"
                    }
                }

                //controllers
                RowLayout {
                    Layout.fillWidth: true
                    Layout.preferredHeight: 36
                    spacing: 8

                    Item {
                        Layout.fillWidth: true
                    }

                    Rectangle {
                        Layout.preferredWidth: 28
                        Layout.preferredHeight: 28
                        radius: 14
                        color: prevMouse.containsMouse ? "black" : "transparent"
                        visible: root.player ? root.player.canGoPrevious : false

                        Text {
                            anchors.centerIn: parent
                            text: "󰒮"
                            font.family: "CaskaydiaCove NF"
                            font.pixelSize: 16
                            color: "white"
                        }

                        MouseArea {
                            id: prevMouse
                            anchors.fill: parent
                            hoverEnabled: true
                            cursorShape: Qt.PointingHandCursor
                            onClicked: if (root.player) {
                                root.player.previous();
                            }
                        }
                    }

                    Rectangle {
                        Layout.preferredWidth: 36
                        Layout.preferredHeight: 36
                        radius: 18
                        color: playMouse.containsMouse ? "white" : "silver"

                        Text {
                            anchors.centerIn: parent
                            text: root.isPlaying ? "󰏤" : "󰐊"
                            font.family: "CaskaydiaCove NF"
                            font.pixelSize: 18
                            color: "black"
                        }

                        MouseArea {
                            id: playMouse
                            anchors.fill: parent
                            hoverEnabled: true
                            cursorShape: Qt.PointingHandCursor
                            onClicked: if (root.player) {
                                root.player.togglePlaying();
                            }
                        }
                    }

                    Rectangle {
                        Layout.preferredWidth: 28
                        Layout.preferredHeight: 28
                        radius: 14
                        color: nextMouse.containsMouse ? "black" : "transparent"
                        visible: root.player ? root.player.canGoNext : false

                        Text {
                            anchors.centerIn: parent
                            text: "󰒭"
                            font.family: "CaskaydiaCove NF"
                            font.pixelSize: 16
                            color: "white"
                        }

                        MouseArea {
                            id: nextMouse
                            anchors.fill: parent
                            hoverEnabled: true
                            cursorShape: Qt.PointingHandCursor
                            onClicked: if (root.player) {
                                root.player.next();
                            }
                        }
                    }

                    Item {
                        Layout.fillWidth: true
                    }
                }
            }
        }
    }

    HoverHandler {
        onHoveredChanged: root.isHovered = hovered
    }

    //second = > min:s
    function formatTime(seconds) {
        if (!seconds || seconds < 0) {
            return "0:00";
        }
        const mins = Math.floor(seconds / 60);
        const secs = Math.floor(seconds % 60);
        return mins + ":" + (secs < 10 ? "0" : "") + secs;
    }
}
